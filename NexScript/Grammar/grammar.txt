STATEMENT   : "RETURN" EXPR
            | "CONTINUE"
            | "BREAK"
            | EXPR

EXPR        : "SET" IDENTIFIER "=" EXPR
            | COMP_EXPR (("AND" | "OR") COMP_EXPR)*

COMP_EXPR   : ARITH_EXPR (("==" | "<" | ">" | "<=" | ">=" | "!=") ARITH_EXPR)*
            | "NOT" COMP_EXPR

ARITH_EXPR  : TERM (("+" | "-") TERM)*

TERM        : FACTOR (("*" | "/") FACTOR)*

FACTOR      : ("+" | "-") FACTOR
            | POWER

POWER       : CALL ("^" FACTOR)*

CALL        : ATOM ("(" (EXPR ("," EXPR)*)? ")")?

ATOM        : INT | FLOAT | STRING | IDENTIFIER
            | "(" EXPR ")"
            | LIST_EXPR
            | IF_EXPR
            | FOR_EXPR
            | WHILE_EXPR
            | FUNC_DEF
            | LIST_INDEX

LIST_INDEX  : IDENTIFIER "/" INT

LIST_EXPR   : "[" (EXPR ("," EXPR)*) "]"

IF_EXPR     : "IF" EXPR "THEN" STATEMENT ("ELIF" EXPR "THEN" STATEMENT)* ("ELSE" STATEMENT)? "END"

FOR_EXPR    : "FOR" IDENTIFIER "=" EXPR "TO" EXPR ("STEP" EXPR)? "THEN" STATEMENT "END"

WHILE_EXPR  : "WHILE" EXPR "THEN" STATEMENT "END"

FUNC_DEF    : "FUNC" IDENTIFIER "(" (IDENTIFIER ("," IDENTIFIER)*)? ")" ("ARROW" EXPR)? "END"
